/*
 * Copyright (C) Niklaus F.Schen.
 */
@load_conf()
{
    f = $File;
    f.open('./config.json', 'r');
    conf = _mln_json_decode(f.read(f.size()));
    f.close();
    return conf;
}

@load_task(&tasks, path)
{
    files = _mln_lsdir(path);
    n = _mln_size(files);
    for (i = 0; i < n; ++i) {
        if (files[i][0] == '.')
            continue;
        fi
        if (_mln_isdir(path+'/'+files[i])) {
            _load_task(tasks, path+'/'+files[i]);
        } else if (_mln_reg_equal('.*\.json$', files[i])) {
            f = $File;
            f.open(path+'/'+files[i], 'r');
            conf = _mln_json_decode(f.read(f.size()));
            f.close();
            if (!conf || !_mln_has(conf, 'period')) continue; fi
            if (!_mln_has(conf, 'data'))
                conf['data'] = [];
            fi
            conf['data']['PWD'] = path;
            conf['base'] = _mln_time();
            tasks[conf['name']] = conf;
        } fi
    }
    return tasks;
}

conf = load_conf();
for (i = 0; i < conf['concurrency']; ++i) {
    mln_eval('worker.mln');
}

readyq = [];
runq = [];
procq = [];
logq = [];
tasks = [];
delta = 0;
load_task(tasks, conf['path']);

while (true) {
    runq = [];
    deps = [];
    n = mln_size(tasks);
    now = mln_time();
    for (i = 0; i < n; ++i) {
        next = mln_cron(tasks[i]['period'], tasks[i]['base']/60*60+mln_int(conf['delta_sec']));
        name = tasks[i]['name'];
        if (next <= now+delta+conf['delta_sec']+1 && !mln_has(readyq, name) && (!mln_has(logq, name) || logq[name] < next)) {
            logq[name] = next;
            readyq[name] = tasks[i];
        } fi
    }
    keys = mln_keys(readyq);
    n = mln_size(keys);
    for (i = 0; i < n; ++i) {
        if (mln_has(procq, keys[i]))
            continue;
        fi
        add = true;
        arr = readyq[keys[i]]['deps'];
        m = mln_size(arr);
        for (j = 0; j < m; ++j) {
            if (mln_has(readyq, arr[j]) || mln_has(procq, arr[j])) {
                add = false;
                break;
            } fi
        }
        if (add) {
            runq[keys[i]] = readyq[keys[i]];
            mln_msg_queue_send('msg', keys[i]+'-'+mln_base64(mln_replace(mln_merge(readyq[keys[i]]['data'], conf['data']), readyq[keys[i]]['cmd']), 'encode'));
        } fi
    }
    procq = mln_merge(procq, runq);
    readyq = mln_key_diff(readyq, runq);
    runq = [];
    for (i = 0; i < conf['concurrency']; ++i) {
        msg = mln_msg_queue_recv('ack', 1);
        if (msg != nil) {
            runq[msg] = 1;
            tasks[msg]['base'] = mln_time()+mln_int(conf['delta_sec']);
        } fi
    }
    procq = mln_key_diff(procq, runq);
    delta = mln_time() - now;
}
